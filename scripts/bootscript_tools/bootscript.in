setenv bootlabel "Android 11"

echo Start boot.scr booting.

echo "  Init default values"

setenv heartbeat 1
setenv overlays "uart0"
setenv overlays_resize 16384
setenv lcd_density 240

echo "  Boot sequence"
if test "$${devnum}" = 0 ; then
	setenv media emmc
	setenv boot_device fe310000.sdhci,fe330000.nandc
else
	setenv media sd
	setenv boot_device fe2b0000.dwmmc,fe330000.nandc
fi

setenv fdt_addr_r 0x0a100000
setenv ini_addr_r 0x40010000
setenv dtbo_addr_r 0x40200000
setenv kernel_addr_c 0x4080000
setenv kernel_addr_r 0x00280000
setenv ramdisk_addr_r 0x0a200000
setenv loadaddr 0x10000000

if bcb load $$devnum misc; then
	# valid BCB found
	if bcb test command = bootonce-bootloader; then
		# Bootloader boot
		bcb clear command; bcb store;
		fastboot usb 0
	elif bcb test command = boot-recovery; then
		# Recovery boot
		setenv partnum ${_recovery_part}
		setenv init_args "firmware_class.path=/vendor/etc/firmware init=/init"
	else
		# Normal boot
		setenv partnum ${_boot_part}
		setenv init_args "firmware_class.path=/vendor/etc/firmware init=/init rootwait ro"
	fi

	echo "  Load booting image"
	setbootdev $$devtype $$devnum

	part start $$devtype $$devnum $$partnum boot_start
	part size $$devtype $$devnum $$partnum boot_size

	mmc dev $$devnum

	mmc read $$loadaddr $$boot_start $$boot_size

	load $$devtype $$devnum:3 $$ini_addr_r config.ini
	ini generic $$ini_addr_r

	echo "  Load Device Tree"
	load $$devtype $$devnum:3 $$fdt_addr_r rockchip/rk3568-odroid-m1.dtb

	fdt addr $$fdt_addr_r
	fdt resize $$overlays_resize

	for overlay in $$overlays; do
		load $$devtype $$devnum:3 $$dtbo_addr_r rockchip/overlays/odroidm1/$$overlay.dtbo
		fdt apply $$dtbo_addr_r

		ini $$overlay $$ini_addr_r
	done

	if test $$heartbeat = "0"; then
		fdt set /leds/work linux,default-trigger "none";
	fi

	echo "  Set booting arguments"
	setenv secure_args "androidboot.selinux=permissive androidboot.veritymode=enforcing androidboot.verifiedbootstate=orange"
	setenv io_args "androidboot.baseband=N/A androidboot.wificountrycode=${_wifi_country} console=ttyFIQ0 androidboot.console=ttyFIQ0 pci=nomsi"
	setenv mtd_args "mtdparts=${_mtd}"

	setenv android_args "androidboot.lcd_density=$$lcd_density"

	setenv bootargs "storagemedia=$$media androidboot.storagemedia=$$media androidboot.mode=normal androidboot.dtb_idx=0 androidboot.slot_suffix= androidboot.serialno=$${serial\#} androidboot.boot_devices=$$boot_device androidboot.hardware=odroid loop.max_part=7 buildvariant=${_variant} $${secure_args} $${io_args} $${init_args} $${mtd_args} $${android_args} $${user_def_args}"

	bootm $$loadaddr
else
	echo Media corrupted.
fi
